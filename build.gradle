plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.18'
    id 'io.spring.dependency-management' version '1.0.15.RELEASE'
}

group = 'com.polarbookshop'
version = '0.0.1-SNAPSHOT'

java {
    sourceCompatibility = '17'
}

repositories {
    mavenCentral()
}

//注解处理器
configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

ext {
    // spring cloud的版本
    set('springCloudVersion', "2021.0.8")
    // 指定Testcontainers版本，用于配置测试用、轻量级的postgreSql容器
    set('testcontainersVersion',"1.17.3")
}

dependencies {
    //引入lombok
    compileOnly 'org.projectlombok:lombok:1.18.30'
    annotationProcessor 'org.projectlombok:lombok:1.18.30','org.springframework.boot:spring-boot-configuration-processor'
    testCompileOnly 'org.projectlombok:lombok:1.18.30'
    testAnnotationProcessor 'org.projectlombok:lombok:1.18.30'

    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-validation'
    // 使用spring cloud config client
    implementation 'org.springframework.cloud:spring-cloud-starter-config'
    // 使用重试模式
    implementation 'org.springframework.retry:spring-retry'
    // 使用配置刷新
    implementation 'org.springframework.boot:spring-boot-starter-actuator'

    // 引入spring data jdbc
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    // 提供postgreSql驱动
    runtimeOnly 'org.postgresql:postgresql'

    //引入flyway，最新版本不支持postgreSql 14.4
    implementation 'org.flywaydb:flyway-core:9.22.3'
    // 引入OAuth2资源服务器
    implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    //响应式web框架webflux
    testImplementation 'org.springframework.boot:spring-boot-starter-webflux'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    // 容器化管理测试用postgreSql数据库
    testImplementation 'org.testcontainers:postgresql'
}

dependencyManagement {
    imports {
        // spring cloud依赖管理的BOM
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        mavenBom "org.testcontainers:testcontainers-bom:${testcontainersVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

// 使用buildpacks构建docker镜像的配置
bootBuildImage {
    builder = "docker.io/paketobuildpacks/builder-jammy-base:0.4.302"
    // 没有指定label，默认latest
    imageName = "${project.name}"
    // 指定java版本
    environment = ["BP_JVM_VERSION" : "17.*"]

    // 构建镜像时，同时推送至远程容器注册中心
//    docker {
//        publishRegistry {
//            username = project.findProperty("registryUsername")
//            password = project.findProperty("registryToken")
//            // 启动参数形式无法识别为字符串
//            url = "ghcr.io"
//        }
//    }
}
